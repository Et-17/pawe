// Define the languages to be used in this file

languages {
    ProtoIndoEuropean;
    PreProtoGermanic;
	EarlyProtoGermanic;
}

// Establish some basic distinctions

parameters {
    type {consonant; vowel}
    length {short; halflong; long; overlong}
}

features {
    syllabic;
    stressed; // we will only mark nuclei as stressed
}

// Establish necessary information for consonants

parameters {
    place {bilabial; dental; alveolar; post_alveolar; palatal; palatovelar; laryngeal; velar; pharyngeal}
    manner {nasal; plosive; fricative; approximant; tap; trill}
    pieLaryngeal {neutral; aColoring; oColoring}
}

features {
    voiced;
    lateral;
    labialized;
	aspirated;
}

characters {
	CONSONANT [+type.consonant +length.short -lateral -labialized -voiced -syllabic -stressed];
	
	NASAL [CONSONANT +manner.nasal +voiced];
	m [NASAL +place.bilabial];
	n [NASAL +place.alveolar];

	PLOSIVE [CONSONANT +manner.plosive];
	p [PLOSIVE +place.bilabial];
	b [p +voiced];
	t [PLOSIVE +place.alveolar];
	d [t +voiced];
	k [PLOSIVE +place.velar];
	g [k +voiced];

	FRICATIVE [CONSONANT +manner.fricative];
	f [FRICATIVE +place.bilabial];
	v [f +voiced];
	s [FRICATIVE +place.alveolar];
	z [s +voiced];
	x [FRICATIVE +place.velar];
	ɣ [x +voiced];
	LARYNGEAL [FRICATIVE +place.laryngeal];
	h₁ [LARYNGEAL +pieLaryngeal.neutral];
	h₂ [LARYNGEAL +pieLaryngeal.aColoring];
	h₃ [LARYNGEAL +pieLaryngeal.oColoring];

	APPROXIMANT [CONSONANT +manner.approximant +voiced];
	l [APPROXIMANT +place.alveolar +lateral];
	w [APPROXIMANT +place.velar +labialized];
	y [APPROXIMANT +place.palatal];
}

// Establish necessary information for vowels

parameters {
    height {close; near_close; close_mid; mid; open_mid; near_open; open}
    backness {front; near_front; central; near_back; back}
}

features {
    rounded;
}

characters {
	ɑ [+type.vowel +height.open +backness.back -rounded +syllabic +length.short -stressed];
    a [+type.vowel +height.open +backness.central -rounded +syllabic +length.short -stressed];
	i [+type.vowel +height.close +backness.front -rounded +syllabic +length.short -stressed];
	u [+type.vowel +height.close +backness.back +rounded +syllabic +length.short -stressed];
	e [+type.vowel +height.close_mid +backness.front -rounded +syllabic +length.short -stressed];
	o [+type.vowel +height.close_mid +backness.back +rounded +syllabic +length.short -stressed];
	ó [o +stressed];
	ə [+type.vowel +height.mid +backness.central -rounded +syllabic +length.short -stressed];
}

// Establish rules for evolution
// The rules that I use in this example file are from various Wikipedia pages

evolve ProtoIndoEuropean to PreProtoGermanic {
	// Allophonic colouring of /e/ adjacent to laryngeal consonants
	e > a / h₂ _;
	e > a / _ h₂;
	e > o / h₃ _;
	e > o / _ h₃;

	// Merging of PIE palatovelar and velar plosives
	1(+place.palatovelar +manner.plosive) > [1 +place.velar];

	// Epenthesis of /u/ before syllabic sonorants
	1(+type.consonant +syllabic) > u [1 -syllabic];

	// Processing inter-dental epenthetic /s/ (idk exactly how this works)
	(+place.dental) s (+place.dental) > [s +length.long];

	// Shortening of geminate consonants after a consonant or a long vowel	
	1(+type.consonant +length.long) > [1 +length.short] / (+type.consonant) _;
	1(+type.consonant +length.long) > [1 +length.short] / (+type.vowel +length.long) _;

	// Lengthening of word-final long vowels
	1(+type.vowel +length.long) > [1 +length.overlong] / _ #;

	// Loss of laryngeals, phonemicising the allophones of /e/
	(+place.laryngeal) > / # _ (+type.consonant);
	1(+type.vowel) (+place.laryngeal) (+type.vowel) > [1 +length.overlong];
	1(+type.vowel) (+place.laryngeal) > [1 +length.long];
	(+place.laryngeal) > / _ (+type.vowel);

	// Cowgill's law
	h₃ > g / (+type.vowel) _ w;
	h₃ > g / (+manner.approximant) _ w;
	h₃ > g / (+manner.nasal) _ w;

	// Vocalization of remaining laryngeals
	(+place.laryngeal) > ə;

	// Labialization of velars
	1(+place.velar) w > [1 +labialized];

	// Delabialization of labiovelars
	1(+place.velar +labialized) > [1 -labialized] / _ u;
	1(+place.velar +labialized) > [1 -labialized] / u _;
	1(+place.velar +labialized) > [1 -labialized] / _ t;
}

evolve PreProtoGermanic to EarlyProtoGermanic {
	// Loss of word-final non-close short vowels, including preceeding /j/ or /w/
	// When the lost vowel was accented, accent shifted to the preceeding syllable
	1(+type.vowel) > [1 +stressed] / _ (-type.vowel)* (+type.vowel -height.close +length.short) #;
	y (+type.vowel -height.close +length.short) > / _ #;
	w (+type.vowel -height.close +length.short) > / _ #;
	(+type.vowel -height.close +length.short) > / _ #;
	// Single-syllable words were not affected, but clitics here, and I don't
	// know how to implement that in this example

	// Grimm's law <3
	// Voiceless plosives become fricatives, unless preceeded by an obstruent
	1(+type.consonant -voiced +manner.plosive) > [1 +manner.fricative] / (-manner.plosive -manner.fricative) _;
	// Voiced plosives are devoiced
	1(+type.consonant +voiced +manner.plosive) > [1 -voiced];
	// Aspirated plosives become voiced plosives, fricatives between vowels
	1(+type.consonant +voiced +manner.plosive +aspirated) > [1 -aspirated +manner.fricative] / (+type.vowel) _ (+type.vowel);
	1(+type.consonant +voiced +manner.plosive +aspirated) > [1 -aspirated];

	// Verner's law
	1(+manner.fricative -voiced) > [1 +voiced] / (+type.vowel -stressed) _;
	1(+manner.fricative -voiced) > [1 +voiced] / (+type.vowel -stressed) (+manner.nasal) _;
	1(+manner.fricative -voiced) > [1 +voiced] / (+type.vowel -stressed) (+manner.approximant) _;

	// All words become stressed on their first syllable
	1(+syllabic) > [1 -stressed];
	1(+syllabic) > [1 +stressed] / # (-type.vowel)* _;

	// Unstressed /owo/ > /oː/
	o w o > [o +length.overlong];

	// Unstressed /ew/ > /ow/ before a consonant or word-finally
	e > o / _ w (+type.consonant);
	e > o / _ w #;

	// Unstressed /e/ > /i/ except before /r/
	e > i;
	e y > [i +length.long];

	// Unstressed /ji/ > /i/
	i y i > [i +length.overlong];
	y i > i;

	// Merging of non-close back vowels
	1(+height.close_mid +backness.back) > [ɑ 1 +height.open +backness.back -rounded];
	1(+height.open +backness.central) > [ɑ 1 +height.open +backness.back -rounded];
}
