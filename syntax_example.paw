// Define the languages to be used in this file

languages {
    ProtoIndoEuropean;
    PreProtoGermanic;
	EarlyProtoGermanic;
}

// Establish some basic distinctions

parameters {
    type {consonant; vowel}
    length {short; halflong; long; overlong}
}

features {
    syllabic;
    stressed; // we will only mark vowels as stressed
}

// Establish necessary information for consonants

parameters {
    place {bilabial; dental; alveolar; post_alveolar; palatal; palatovelar; laryngeal; velar; pharyngeal}
    manner {nasal; plosive; fricative; approximant; tap; trill}
    pieLaryngeal {neutral; aColoring; oColoring}
}

features {
    voiced;
    lateral;
    labialized;
	aspirated;
}

characters {
    m [+type.consonant +place.bilabial +manner.nasal -lateral +voiced +length.short];
	n [m +place.alveolar];
    t [+type.consonant +place.alveolar +manner.plosive -lateral -voiced +length.short];
    s [+type.consonant +place.alveolar +manner.fricative -lateral -voiced +length.short];
    l [+type.consonant +place.alveolar +manner.approximant +lateral +voiced +length.short];
    h₁ [+type.consonant +place.laryngeal +manner.fricative -lateral -voiced +length.short +pieLaryngeal.neutral];
    h₂ [+type.consonant +place.laryngeal +manner.fricative -lateral -voiced +length.short +pieLaryngeal.aColoring];
    h₃ [+type.consonant +place.laryngeal +manner.fricative -lateral -voiced +length.short +pieLaryngeal.oColoring];
    w [+type.consonant +place.velar +manner.approximant +labialized -lateral +voiced +length.short];
	k [+type.consonant +place.velar +manner.plosive -lateral -voiced +length.short];
	y [+type.consonant +place.palatal +manner.approximant -labialized -lateral +voiced +length.short];
}

// Establish necessary information for vowels

parameters {
    height {close; near_close; close_mid; mid; open_mid; near_open; open}
    backness {front; near_front; central; near_back; back}
}

features {
    rounded;
}

characters {
	ɑ [+type.vowel +height.open +backness.back -rounded +syllabic +length.short -stressed];
    a [+type.vowel +height.open +backness.central -rounded +syllabic +length.short -stressed];
	i [+type.vowel +height.close +backness.front -rounded +syllabic +length.short -stressed];
	u [+type.vowel +height.close +backness.back +rounded +syllabic +length.short -stressed];
	e [+type.vowel +height.close_mid +backness.front -rounded +syllabic +length.short -stressed];
	o [+type.vowel +height.close_mid +backness.back +rounded +syllabic +length.short -stressed];
	ó [o +stressed];
	ə [+type.vowel +height.mid +backness.central -rounded +syllabic +length.short -stressed];
}

// Establish rules for evolution
// The rules that I use in this example file are from various Wikipedia pages

evolve ProtoIndoEuropean to PreProtoGermanic {
	// Allophonic colouring of /e/ adjacent to laryngeal consonants
	e > a / h₂ _;
	e > a / _ h₂;
	e > o / h₃ _;
	e > o / _ h₃;

	// Merging of PIE palatovelar and velar plosives
	1(+place.palatovelar +manner.plosive) > [1 +place.velar];

	// Epenthesis of /u/ before syllabic sonorants
	1(+type.consonant +syllabic) > u [1 -syllabic];

	// Processing inter-dental epenthetic /s/ (idk exactly how this works)
	(+place.dental) s (+place.dental) > [s +length.long];

	// Shortening of geminate consonants after a consonant or a long vowel	
	1(+type.consonant +length.long) > [1 +length.short] / (+type.consonant) _;
	1(+type.consonant +length.long) > [1 +length.short] / (+type.vowel +length.long) _;

	// Lengthening of word-final long vowels
	1(+type.vowel +length.long) > [1 +length.overlong] / _ #;

	// Loss of laryngeals, phonemicising the allophones of /e/
	(+place.laryngeal) > / # _ (+type.consonant);
	1(+type.vowel) (+place.laryngeal) (+type.vowel) > [1 +length.overlong];
	1(+type.vowel) (+place.laryngeal) > [1 +length.long];
	(+place.laryngeal) > / _ (+type.vowel);

	// Vocalization of remaining laryngeals
	(+place.laryngeal) > ə;

	// Labialization of velars
	1(+place.velar) w > [1 +labialized];

	// Delabialization of labiovelars
	1(+place.velar +labialized) > [1 -labialized] / _ u;
	1(+place.velar +labialized) > [1 -labialized] / u _;
	1(+place.velar +labialized) > [1 -labialized] / _ t;
}

evolve PreProtoGermanic to EarlyProtoGermanic {
	// Loss of word-final non-close short vowels, including preceeding /j/ or /w/
	y (+type.vowel -height.close +length.short) > / _ #;
	w (+type.vowel -height.close +length.short) > / _ #;
	(+type.vowel -height.close +length.short) > / _ #;

	// Grimm's law <3
	// Voiceless plosives become fricatives, unless preceeded by an obstruent
	1(+type.consonant -voiced +manner.plosive) > [1 +manner.fricative] / (-manner.plosive -manner.fricative -manner.nasal) _;
	// Voiced plosives are devoiced
	1(+type.consonant +voiced +manner.plosive) > [1 -voiced];
	// Aspirated plosives become voiced plosives, fricatives between vowels
	1(+type.consonant +voiced +manner.plosive +aspirated) > [1 -aspirated +manner.fricative] / (+type.vowel) _ (+type.vowel);
	1(+type.consonant +voiced +manner.plosive +aspirated) > [1 -aspirated];

	// Verner's law
	1(+manner.fricative -voiced) > [1 +voiced] / (+type.vowel -stressed) _;
	1(+manner.fricative -voiced) > [1 +voiced] / (+type.vowel -stressed) (+manner.nasal) _;
	1(+manner.fricative -voiced) > [1 +voiced] / (+type.vowel -stressed) (+manner.approximant) _;

	// Unstressed /owo/ > /oː/
	o w o > [o +length.overlong];

	// Unstressed /ew/ > /ow/ before a consonant or word-finally
	e > o / _ w (+type.consonant);
	e > o / _ w #;

	// Unstressed /e/ > /i/ except before /r/
	e > i;
	e y > [i +length.long];

	// Unstressed /ji/ > /i/
	i y i > [i +length.overlong];
	y i > i;

	// Merging of non-close back vowels
	1(+height.close_mid +backness.back) > [ɑ 1 +height.open +backness.back -rounded];
	1(+height.open +backness.central) > [ɑ 1 +height.open +backness.back -rounded];
}
