// Define the languages to be used in this file

languages {
    ProtoIndoEuropean;
    PreProtoGermanic;
	EarlyProtoGermanic;
	LateProtoGermanic;
}

// Establish some basic distinctions

parameters {
    type {consonant; vowel}
    length {short; halflong; long; overlong}
}

features {
    syllabic;
    stressed; // we will only mark nuclei as stressed
	preserve; // this is for preventing rule application if needes, but ensure it's turned off when you're done
}

// Establish necessary information for consonants

parameters {
    place {bilabial; dental; alveolar; post_alveolar; palatal; palatovelar; laryngeal; velar; pharyngeal}
    manner {nasal; plosive; fricative; approximant; tap; trill}
    pieLaryngeal {neutral; aColoring; oColoring}
}

features {
    voiced;
    lateral;
    labialized;
	aspirated;
}

characters {
	CONSONANT [+type.consonant +length.short -lateral -labialized -voiced -aspirated -syllabic -stressed];
	
	NASAL [CONSONANT +manner.nasal +voiced];
	m [NASAL +place.bilabial];
	n [NASAL +place.alveolar];

	PLOSIVE [CONSONANT +manner.plosive];
	p [PLOSIVE +place.bilabial];
	b [p +voiced];
	t [PLOSIVE +place.dental];
	d [t +voiced];
	k [PLOSIVE +place.velar];
	g [k +voiced];

	FRICATIVE [CONSONANT +manner.fricative];
	f [FRICATIVE +place.bilabial];
	v [f +voiced];
	s [FRICATIVE +place.alveolar];
	z [s +voiced];
	x [FRICATIVE +place.velar];
	ɣ [x +voiced];
	þ [FRICATIVE +place.dental];
	ð [þ +voiced];
	h [FRICATIVE +place.laryngeal];

	APPROXIMANT [CONSONANT +manner.approximant +voiced];
	l [APPROXIMANT +place.alveolar +lateral];
	r [APPROXIMANT +place.alveolar];
	y [APPROXIMANT +place.palatal];
	w [APPROXIMANT +place.velar +labialized];
}

diacritics {
	H₁ +pieLaryngeal.neutral;
	H₂ +pieLaryngeal.aColoring;
	H₃ +pieLaryngeal.oColoring;

	ō +length.long;
	ô +length.overlong;
	// Because pawe can't yet have diacritics mean different things, we will be
	// using +stressed to represent palatoovelars
	ó +stressed;

	Cʰ +aspirated;
	Cʷ +labialized;
	n̩ +syllabic;
}

// Establish necessary information for vowels

parameters {
    height {close; near_close; close_mid; mid; open_mid; near_open; open}
    backness {front; near_front; central; near_back; back}
}

features {
    rounded;
	nasalized;
}

characters {
	V [+type.vowel -rounded +syllabic +length.short -stressed -nasalized];

	a [V +height.open +backness.central];
	ɑ [V +height.open +backness.back];
	i [V +height.close +backness.front];
	u [V +height.close +backness.back +rounded];
	e [V +height.close_mid +backness.front];
	o [V +height.close_mid +backness.back +rounded];
	ɔ [V +height.open_mid +backness.back +rounded];
	ə [V +height.mid +backness.central];
}

diacritics {
	ǫ +nasalized;
}

// Establish rules for evolution
// The rules that I use in this example file are from various Wikipedia pages

evolve ProtoIndoEuropean to PreProtoGermanic {
	// Allophonic colouring of /e/ adjacent to laryngeal consonants
	1(+height.close_mid +backness.front) > [1 +height.open +backness.central] / h₂ _;
	1(+height.close_mid +backness.front) > [1 +height.open +backness.central] / _ h₂;
	1(+height.close_mid +backness.front) > [1 +backness.back +rounded] / h₃ _;
	1(+height.close_mid +backness.front) > [1 +backness.back +rounded] / _ h₃;

	// Merging of PIE palatovelar and velar plosives
	// Because we need ◌́ to represent stress, and we can't specify different
	// diacritic interpretations yet, palatovelars are just stressed velars.
	1(+place.velar +stressed +manner.plosive) > [1 -stressed];

	// Epenthesis of /u/ before syllabic sonorants
	1(+type.consonant +syllabic) > u [1 -syllabic];

	// Processing inter-dental epenthetic /s/ (idk exactly how this works)
	(+place.dental) s (+place.dental) > [s +length.long];

	// Shortening of geminate consonants after a consonant or a long vowel	
	1(+type.consonant +length.long) > [1 +length.short] / (+type.consonant) _;
	1(+type.consonant +length.long) > [1 +length.short] / (+type.vowel +length.long) _;

	// Lengthening of word-final long vowels
	1(+type.vowel +length.long) > [1 +length.overlong] / _ #;

	// Loss of laryngeals, phonemicising the allophones of /e/
	(+place.laryngeal) > / # _ (+type.consonant);
	1(+type.vowel) (+place.laryngeal) (+type.vowel) > [1 +length.overlong];
	1(+type.vowel) (+place.laryngeal) > [1 +length.long];
	(+place.laryngeal) > / _ (+type.vowel);

	// Cowgill's law
	h₃ > g / (+type.vowel) _ w;
	h₃ > g / (+manner.approximant) _ w;
	h₃ > g / (+manner.nasal) _ w;

	// Vocalization of remaining laryngeals
	(+place.laryngeal) > ə;

	// Labialization of velars
	1(+place.velar) w > [1 +labialized];

	// Delabialization of labiovelars
	1(+place.velar +labialized) > [1 -labialized] / _ u;
	1(+place.velar +labialized) > [1 -labialized] / u _;
	1(+place.velar +labialized) > [1 -labialized] / _ t;
}

evolve PreProtoGermanic to EarlyProtoGermanic {
	// Loss of word-final non-close short vowels, including preceeding /j/ or /w/
	// When the lost vowel was accented, accent shifted to the preceeding syllable
	1(+type.vowel) > [1 +stressed] / _ (-type.vowel)* (+type.vowel -height.close +length.short) #;
	y (+type.vowel -height.close +length.short) > / _ #;
	w (+type.vowel -height.close +length.short) > / _ #;
	(+type.vowel -height.close +length.short) > / _ #;
	// Single-syllable words were not affected, but clitics where, and I don't
	// know how to implement that in this example

	// Grimm's law <3
	// Voiceless plosives become fricatives, unless preceeded by an obstruent
	1(+type.consonant -voiced +manner.plosive) > [1 +manner.fricative] / (-type.consonant) _;
	1(+type.consonant -voiced +manner.plosive) > [1 +manner.fricative] / (+type.consonant -manner.plosive -manner.fricative) _;
	1(+type.consonant -voiced +manner.plosive) > [1 +manner.fricative] / # _;
	// Voiced plosives are devoiced
	1(+type.consonant +voiced +manner.plosive -aspirated) > [1 -voiced];
	// Aspirated plosives become voiced plosives, fricatives between vowels
	1(+type.consonant +voiced +manner.plosive +aspirated) > [1 -aspirated +manner.fricative] / (+type.vowel) _ (+type.vowel);
	1(+type.consonant +voiced +manner.plosive +aspirated) > [1 -aspirated];

	// Verner's law
	1(+manner.fricative -voiced) > [1 +voiced] / (+type.vowel -stressed) _;
	1(+manner.fricative -voiced) > [1 +voiced] / (+type.vowel -stressed) (+manner.nasal) _;
	1(+manner.fricative -voiced) > [1 +voiced] / (+type.vowel -stressed) (+manner.approximant) _;

	// All words become stressed on their first syllable
	1(+syllabic +stressed) > [1 -stressed] / (+syllabic) (-syllabic)* _;
	1(+syllabic -stressed) > [1 +stressed] / # (-syllabic)* _;

	// Unstressed /owo/ > /oː/
	o w o > [o +length.overlong];

	// Unstressed /ew/ > /ow/ before a consonant or word-finally
	e > o / _ w (+type.consonant);
	e > o / _ w #;

	// Unstressed /e/ > /i/ except before /r/
	e y > [i +length.long] / _ r!;
	e > i / _ r!;

	// Unstressed /ji/ > /i/
	i y i > [i +length.overlong];
	y i > i;

	// Merging of non-close back vowels
	1(-height.close +backness.back) > [1 +height.open -rounded];
	1(+height.open +backness.central) > [1 +backness.back];

	// Reanalyze /Vw/ and /Vj/ as /Vu/ and /Vi/
	// w > [u -syllabic] / (+syllabic) _ (-syllabic);
	// w > [u -syllabic] / (+syllabic) _ #;
	// y > [i -syllabic] / (+syllabic) _ (-syllabic);
	// y > [i -syllabic] / (+syllabic) _ #;
}

evolve EarlyProtoGermanic to LateProtoGermanic {
	// /m/ > /n/ before dental consonants and word finally
	m > n / _ #;
	m > n / _ (+place.dental);

	// Word-final /n/ is lost after unstressed syllables, causing nasalization
	1(+type.vowel -stressed) n > [1 +nasalized] / _ #;

	// Nasal /ẽː/ is lowered to /ɑ̃ː/
	ę̄ > ɑ̨̄;

	// Elimination of /ə/
	ə > / (+type.consonant) _ (+type.consonant);
	ə > ɑ;

	// Assimilation of sonorants
	w > n / n _;
	n > l / l _;
	z > m / _ m;

	// Loss of word-final /t/ after unstressed syllables
	t > / (+syllabic -stressed) (-syllabic)* _ #;

	// /ɣʷ/ > /w/
	[ɣ +labialized] > w;

	// Long a is raised
	1(ɑ -length.short) > [1 +height.open_mid +backness.back +rounded];

	// Early i-mutation
	1(+height.close_mid +backness.front +length.short) y > [1 +height.close +length.long];
	e > i / _ (-syllabic)* i;
	e > i / _ (-syllabic -manner.approximant)* y;

	// /e/ > /i/ when followed by a syllable-final nasal
	e > i / _ (+manner.nasal);

	// /j/ is lost between vowels except after /i/ and /w/
	y > [y +preserve] / i _;
	y > [y +preserve] / w _;
	y > / (+type.vowel) _ (+type.vowel);
	1(+preserve) > [1 -preserve];
	

	// /n/ is lost before /x/, causing compensatory lengthening and nasalization
	// of the preceeding vowel
	1(+type.vowel) > [1 +length.long +nasalized] / _ (-syllabic)* n h;
	n > / _ h;
}
